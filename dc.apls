#!/usr/local/bin/dyalogscript MAXWS=12G

⎕IO←1
⎕FX⊃⎕NGET'APLSource/MATERIAL.aplf'1
⎕FX⊃⎕NGET'APLSource/MICRO.aplf'1
⎕FX⊃⎕NGET'APLSource/CPFEH.aplf'1

bcc←'materials/fe_bcc.json5' ⋄ khp←20 ⋄ hsr←10                    ⍝ material
hrd←((575 80 150 40)(1 2))((590 90 100 50)(2 1))                  ⍝ hardening
da iq ci dm b←10 100 0.01 1.25 15 ⋄ file←'dc06rdnd.ctf'           ⍝ microstruture
bc sr wr st ∆t steps←(0 1 1 0 0 0)(1e¯3,5⍴0)0(6⍴0)0.25 1000       ⍝ boundary conditions
tol min max conv←1e¯3 1e¯2 100(0.1 0.9 1.1 1.25) ⋄ grains←1000    ⍝ simulation parameters

I←11 0∘⍕ ⋄ D←11 5∘⍕ ⋄ S←11 ¯5∘⍕ ⍝ integer decimal scientific
SIM←{o n t←⍵ ⋄ e←o↑¨ea ⋄ v i←o↑¨vol mat ⋄ a←o o∘↑¨area ⋄ _←(⊃⍺)TEX e,v i
    SF←{⎕←12↑¨(S¨⍵[1,7+⍳6]),⊂I⍵[14]}
    ⎕←¯12↑¨7↑¨(⊂'time'),('s',¨'11' '22' '33' '23' '13' '12'),⊂'iter'
    r e f←n t bc sr wr st(tol min max conv CPFEH SF)m[i]e a v⊣ts←⎕TS
    w←((2⊃⍺)TEX e,v i),(↓[1]r)(,f)CSV¨2↓⍺
    w,⍨⊂' seconds',⍨D 1e3÷⍨⎕TS-⍥(12⎕DT⊂)ts}
DMN←{H←{i←⍵ ⋄ (⌊dm÷⍨63.8){⊖⍕(⍉⍕⍪(1↓⌊⍵×m[i;⍳⍺])⍴¨'⎕')(⍉⍕⍪(1↓⌊⍵×n[i;⍳⍺])⍴¨'⎕')}÷k}
    k←⍺ ⋄ q m n←⍵ ⋄ ⍕q,⍪H¨⍳≢q}
CSV←{f←⍵,'.csv' ⋄ 0::0↑⎕←(⊃⎕DM)f ⋄ f⊣⍺⎕CSV⍠'IfExists' 'Replace'⊢f}
CSD←{⍺CSV⍨↓⍉((⊂'ang'),,/⍕¨q)⍪{⍵⌿⍨(0∨.≠1↓⊢)⍤1⊢⍵}(dm×1-⍨⍳⊃⌽⍴⍵),⍉⍵}
TEX←{⍺CSV⍨(⊂¨'phi1' 'Phi' 'phi2' 'vol' 'phase'),¨⍵}

⎕←'Reading ',file
    ep ee s←da iq ci MICRO file 25 25 225 225
⎕←'Analizing ',(⍕≢ep),'×',(⍕≢⍉ep),'=',(⍕×/⍴ep),' points'
    p e v q m n x y←b(d←dm MICRO ee)MICRO ep ee s
    ⎕←250 DMN q m n
⎕←'Partitioning by size'
    r←({*(+⌿÷≢)*{⍵⌿⍨0>⍵},⍵}¨x y)<,¨x y
    size←{⍵[⍒⊢/⍵;]}(⍉↑r){⍺,(≢⍵),(*(+⌿÷≢)⍟⍵)}⌸⍥{⍵⌿⍨0<,xy}⍉↑,¨x y(xy←x×y)
    ⎕←'rx' 'ry' 'n' 'x' 'y' 'x×y'⍪size
    p e v q m n←d MICRO(ep+¯1 ¯1↓(1+⍴ep)⍴2⊥⍤1⍉↑r)ee s
    ⎕←50 DMN q m n
    ⎕←('dc_tex'TEX(↓⍉e),v p),'dc_x' 'dc_y'CSD¨m n
⎕←'Creating microstructure input data'
    area←¯3↑mat e vol vuvx vuvy vuvz←r←d MICRO p e v q m n ⋄ ea←↓⍉e
    ⎕←(⍕¨((⊂'mat')('phi1' 'Phi' 'phi2')(⊂'vol'))⍪¨⍪¨10↑¨mat e vol)
    ⎕←≢vol
⎕←'Creating material input data'
    s←khp÷(⊢/size)*÷2 ⋄ h←{n←⎕NS⍬ ⋄ n.(voce lat)←⍵ ⋄ n}¨hrd
    m←{hsr(⍵ MATERIAL)bcc}¨s{m←⎕NS¨⍵ ⋄ m.voce+←⊂4↑⍺ ⋄ m}¨⊂h
    ⎕←(⊢/size)⍪⍉↑∪¨m.H 0
⎕←'Running simulation of ',(⍕steps),' steps with ',(⍕grains),' grains'
    ⎕←'Finished in ','dc_tex0' 'dc_tex1' 'dc_r' 'dc_f'SIM grains steps ∆t
