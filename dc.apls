#!/usr/local/bin/dyalogscript MAXWS=12G

bcc←'materials/fe_bcc.json5' ⋄ khp←20                           ⍝ material
da iq ci dm b←10 100 0.01 1.25 15 ⋄ file←'dc06rdnd.ctf'         ⍝ microstruture
bc sr wr st ∆t steps←(0 1 1 0 0 0)(1e¯3,5⍴0)0(6⍴0)0.1 10       ⍝ boundary conditions
tol min max conv←1e¯3 1e¯2 100(0.1 0.9 1.1 1.25) ⋄ grains←10   ⍝ simulation parameters

⎕IO←1
⎕FX⊃⎕NGET'APLSource/MATERIAL.aplf'1
⎕FX⊃⎕NGET'APLSource/MICRO.aplf'1
⎕FX⊃⎕NGET'APLSource/CPFEH.aplf'1

I←9 0∘⍕ ⋄ D←9 2∘⍕ ⋄ S←9 ¯3∘⍕ ⍝ integer decimal scientific
SIM←{⍺←⍬ ⋄ o n t←⍵ ⋄ e←o↑¨ea ⋄ v i←o↑¨vol mat ⋄ a←o o∘↑¨area
    r e f←n t bc sr wr st(tol min max CPFEH conv)m[i]e a v⊣ts←⎕TS
    _←(↓[1]r)(e,⊂v)(,f)⎕CSV⍠'IfExists' 'Replace'¨⍣(~0∊⍴⍺)⊢⍺
    ⍺,⍨10↑¨(2(I¨⍤↑,S¨⍤↓)⍵),(I¨+⌿¯1↑[2]r),⊂D 1e3÷⍨⎕TS-⍥(12⎕DT⊂)ts}
DMN←{H←{i←⍵ ⋄ (⌊dm÷⍨63.8){⊖⍕(⍉⍕⍪(1↓⌊⍵×m[i;⍳⍺])⍴¨'⎕')(⍉⍕⍪(1↓⌊⍵×n[i;⍳⍺])⍴¨'⎕')}÷k}
    k←⍺ ⋄ q m n←⍵ ⋄ ⍕q,⍪H¨⍳≢q}

⎕←'Reading ',file
    ep ee s←da iq ci MICRO file 25 25 225 225
⎕←'Analizing ',(⍕≢ep),'×',(⍕≢⍉ep),'=',(⍕×/⍴ep),' points'
    p e v q m n x y←b(d←dm MICRO ee)MICRO ep ee s
    ⎕←250 DMN q m n
⎕←'Partitioning by size'
    r←({*(+⌿÷≢)*{⍵⌿⍨0>⍵},⍵}¨x y)<,¨x y
    size←{⍵[⍒⊢/⍵;]}(⍉↑r){⍺,(≢⍵),(*(+⌿÷≢)⍟⍵)}⌸⍥{⍵⌿⍨0<,xy}⍉↑,¨x y(xy←x×y)
    ⎕←'rx' 'ry' 'n' 'x' 'y' 'x×y'⍪size
    p e v q m n←d MICRO(ep+¯1 ¯1↓(1+⍴ep)⍴2⊥⍤1⍉↑r)ee s
    ⎕←50 DMN q m n
⎕←'Creating microstructure input data'
    area←¯3↑mat e vol vuvx vuvy vuvz←r←d MICRO p e v q m n ⋄ ea←↓⍉e
    ⎕←⍕¨((⊂'mat')('phi1' 'Phi' 'phi2')(⊂'vol'))⍪¨⍪¨10↑¨mat e vol
    ⎕←≢vol
⎕←'Creating material input data'
    m←(khp÷(⊢/size)*÷2){n←⎕NS⍵ ⋄ n.z+←⍺ ⋄ n}¨MATERIAL bcc
    ⎕←(⊢/size)⍪⍉↑∪¨m.z
⎕←'Running simulation of ',(⍕steps),' steps with ',(⍕grains),' grains'
    ⎕←¯10↑¨7↑¨'grains' 'steps' '∆time' 'iter' 'seconds'
    ⎕←'dc_r.csv' 'dc_tex.csv' 'dc_f.csv'SIM grains steps ∆t
