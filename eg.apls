#!/usr/local/bin/dyalogscript MAXWS=1G

⎕IO←1 ⋄ ⎕RL←1
⎕FX⊃⎕NGET'APLSource/MATERIAL.aplf'1
⎕FX⊃⎕NGET'APLSource/CPFEH.aplf'1

n←250                                ⍝ units
bcc←'materials/fe_bcc.json5'         ⍝ bcc iron
fcc←'materials/fe_fcc.json5'         ⍝ fcc iron
iu←?n⍴≢m←MATERIAL¨bcc fcc            ⍝ materials
ea vol←{(2×○?3⍴⊂⍵⍴0)((⊢÷+/)?⍵⍴0)}n   ⍝ texture
vuvd←{(⍳3){(⊢÷⍤1 0+/)4+?⍵}¨⊂⍵ ⍵⍴0}n  ⍝ topology

b←0 1 1 0 0 0                        ⍝ stresses
e←1e¯3,5⍴0                           ⍝ strain rate
w←0                                  ⍝ angular velocity
s←6⍴0                                ⍝ stress
tol←1e¯3                             ⍝ tolerance
max←500                              ⍝ max iterations
conv←0.1 0.9 1.25 1.5                ⍝ down up inc dec

I←9 0∘⍕ ⋄ D←9 2∘⍕ ⋄ S←9 ¯3∘⍕ ⍝ integer decimal scientific
SIM←{
    ⍺←⍬ ⋄ o n t←⍵ ⋄ ea1←o↑¨ea ⋄ vol1 iu1←o↑¨vol iu ⋄ vuvd1←o o∘↑¨vuvd
    r ea1 f←m vuvd1 iu1 vol1 ea1 tol max conv CPFEH n t b e w s⊣ts←⎕TS
    _←(↓[1]r)(ea1,⊂vol1)(,f)⎕CSV⍠'IfExists' 'Replace'¨⍣(~0∊⍴⍺)⊢⍺
    ⍺,⍨10↑¨(2(I¨⍤↑,S¨⍤↓)⍵),(I¨+⌿¯2↑[2]r),⊂D 1e3÷⍨⎕TS-⍥(12⎕DT⊂)ts
}

⎕←¯10↑¨7↑¨'grains' 'steps' '∆time' 'iter' 'iterc' 'seconds'
⎕←SIM  50 10 0.05
⎕←SIM 100 10 0.05
⎕←SIM 150 10 0.05
⎕←SIM 200 10 0.05
⎕←SIM 250 10 0.05
⎕←SIM 50 100 0.05
⎕←SIM 50 500 0.05
⎕←'r.csv' 'tex.csv' 'f.csv'SIM 50 1000 0.05
