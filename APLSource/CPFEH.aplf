CPFEH←{
     M←{ a b c d e f←,1 2∘.○⍵                                                                ⍝ matrix from euler angles
         3 3⍴((d×f)-e×a×c)(-(d×c)+e×f×a)(a×b)((f×a)+d×e×c)((d×e×f)-a×c)(-d×b)(b×c)(f×b)e}   
     E←{ p2←(¯3○⍵[3;1]÷⍥(b∘/⊃)⍵[3;2])@((b←⊃0.9999>⍵[3;3])⍨)≠⍨p←¯2○⊃⍵[3;3]                    ⍝ euler angles from matrix
         p1←¯3○(-⍵[1;3]÷⍥(b∘/⊃)⍵[2;3])@(b⍨)(⍵[2;1]÷⍥((~b)∘/⊃)⍵[1;1])@(~b⍨)p ⋄ p1 p p2}
     R←{ i j←(2 1 1)(3 3 2) ⋄ r w←⍵ ⋄ r[⍳3;⍺]←×⍨w[⍺;⍳3] ⋄ r[⍺;3+⍳3]←2*w[i;⍺]×w[j;⍺]          ⍝ rotate voigt vec or matrix
         r[3+⍳3;⍺]←w[⍺;i]×w[⍺;j] ⋄ r[3+⍳3;3+⍺]←w[i[⍺];i]×w[j[⍺];j]+w[j[⍺];i]×w[i[⍺];j]
         r w}{⊃⊃⍺⍺/(⍳3),⊂(6 6⍴≠⍨⊃⍵)⍵}{(⍺⍺⍉⍺)+.×⍵+.×⍵⍵×⍺⍺ ⍺}(⊃⍪⌿,/3 3∘⍴¨2 2⍴1 0.5 2 1)
     W←{ w←0 1 ¯1 1 0 ¯1 1 ¯1 0×(0,⍨(3↓T(-_ ssu)×⍤1 0⊢⍵)-⍺+.×wo+.×⍉⍺)[9⍴⌽⍳4]                 ⍝ rotate orientation matrices
         ⍺+.×i3+(w×1○u)+w+.×(w←w÷u)×1-2○u←(÷2)*⍨+/1 1⍉w←↑{0.5×⍵×3-+.×⍨⍵}¨↓3 3 ⍴w}
     C←{ jv∘←((j∘←vu+.×⍨↑ju+.×⍵)+.×⊢⌹i6-⊢)⍤2↑[÷2]+⌿(↑⍺)+.×⍤2⊢vuvd∘.(+.×)i6-⍵                 ⍝ self-consistent interaction
         (↓[1]+.×∘⌹vu+.×⊢)(↑[÷2]ju)((j+⊢)⌹⍨∘↑j+⊣)⍤2⊢jv∘←(i6-⍤2⊢+.×∘⌹vu+.×i6-⍤2⊢)jv
     }⍣{i1 ic+←1 ⋄ (max=i1)∨tol>w∘←⌈/∊|⍺(-÷⌈⍥|)⍥⊃⍵}∘{i1∘←0 ⋄ ⍵}
     S←{down up inc dec←conv ⋄ down⌈up⌊⍺×inc(÷dec)[1+>/⍵]}{bd so←⍺                           ⍝ solve grain stresses
         esu∘←g×(××hs*⍨|)rsu∘←(msu÷⍤1 0⊢tsu)+.×⍤1⊂¨iu⊆[1]↑[÷2]au+⍵                           ⍝   local slip shear
         ju∘←,⌿ku+t×(⊃+/)¨(tsu÷⍨⍤0 2∘.×⍤1⍨msu)×⍤2 0⊢hs×g×(××(hs-1)*⍨|)rsu ⋄ bu∘←bd C bu      ⍝   local behaviour
         ∆←⊃(⊢+j+.×⊣)/bo(~bo)×so(vo×t)-s e∘←(vu+.×⍨↑)¨⍵(eu←(⍵+.×⍨,⌿ku)+T msu×⍤1 0⊢esu)       ⍝   global error
         ∆u∘←(e-eu)+(↓[1]jv)+.×s-⍵ ⋄ x xu∘←⌈/⍤|¨∆(⌈/⍤|¨∆u) ⋄ d ⍺⍺¨←(xu yu)(x y)              ⍝   local error and factor
         ⍵+⊃d+.×(bu+.×j⌹⍨∆)(↓[1](j+⍤2⊢jv)⌹⍨⍤¯1↑[÷2]∆u)                                       ⍝   local correction
     }⍣{(max=i∘←i+1)∨tol∧.>xo÷⍨y yu∘←x xu}∘{d y yu∘←1 ⋄ i ic∘←0 ⋄ xo∘←⌈/|t×vo ⋄ ⍵}
     _←{((⊃¨((2⍴¨⍳3),(2 3)(1 3)(1 2))⌷¨⊂)2÷⍨⍺⍺∘⍉⍨)⍤2⊢⍵} ⋄ T←{t×,⌿(⊃+/)¨⍵}
     i3 i6←∘.=⍨∘⍳¨3 6 ⋄ bd←(⊂i6)×⍤1¨(2 0 0 1 0 1)(0 2 0 1 1 0)(0 0 2 0 1 1)÷2                ⍝ init
     tol max←⍺⍺ ⋄ conv←⍵⍵ ⋄ n t bo vo wo so←⍺ ⋄ m←∪⊃mat ea area vol←⍵ ⋄ g hs←m.g m.hs        ⍝   parameters
     iu vuvd vu ru←{⍺[⍵](⍵ ⍵∘⌷¨(⊢÷⍤1 0+/)¨area)((⊢÷+/)vol[⍵])(M(⊂⊂⍵)⌷¨ea)}∘⍋⍨m⍳mat           ⍝   microstructure
     ku ssu←m{((↑⍺.k)R⍨⍤2⊢⍵)((↑⍺.ss)(⊢+.×+.×∘⍉)⍤2⊂¨⍵)}iu⊆[1]↑[÷2]ru ⋄ msu←+_ ssu             ⍝   behaviour
     au←6⍴⊂(≢vu)⍴as←ae←6⍴at←0 ⋄ asu←≠⍨tsu←m.z ⋄ fu←i3×⊂(≢vu)⍴1 ⋄ bu←i6 ⋄ (E ru)fu,⍨⊂{        ⍝ simulation
         at ae as au+←t e s,⊂(bd×⊂¨(⊢÷+/)1⌽2×/4⍴1 1⍉fu)((2×so)-vu+.×⍨↑au)S=⍨au               ⍝   solve
         tsu+←m.(⊃H/)↓⍉↑asu(t×esu) ⋄ asu+←t×|esu ⋄ fu+.×⍨←i3+T ssu×⍤2 0⊢esu ⋄ ru W←esu       ⍝   update
         ⍵⍪at,ae,as,i ic}⍣n⊢1 15⍴0
}
