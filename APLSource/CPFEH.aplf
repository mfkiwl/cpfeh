 CPFEH←{
     min inc←0.25 1.5 ⋄ i3 i6←∘.=⍨∘⍳¨3 6 ⋄ bd←(⊂i6)×⍤1¨(2 0 0 1 0 1)(0 2 0 1 1 0)(0 0 2 0 1 1)÷2
     M←{
         A B C a b c←,1 2∘.○⍵
         3 3⍴((a×c)-b×A×C)(-(a×C)+b×c×A)(A×B)((c×A)+a×b×C)((a×b×c)-A×C)(-a×B)(B×C)(c×B)b
     }
     _V←{
         i j←(2 1 1)(3 3 2) ⋄ r←⍵ ⋄ r[⍳3;⍺]←×⍨⍺⍺[⍺;⍳3]
         r[⍺;3+⍳3]←2*⍺⍺[i;⍺]×⍺⍺[j;⍺] ⋄ r[3+⍳3;⍺]←⍺⍺[⍺;i]×⍺⍺[⍺;j]
         r[3+⍳3;3+⍺]←⍺⍺[i[⍺];i]×⍺⍺[j[⍺];j]+⍺⍺[j[⍺];i]×⍺⍺[i[⍺];j] ⋄ r
     }
     R←{⊃⍵ _V/(⍳3),⊂6 6⍴≠⍨⊃⍵}{(⍺⍺⍉⍺)+.×⍵+.×(⊃⍪⌿,/3 3∘⍴¨2 2⍴1 0.5 2 1)∘×⍺⍺ ⍺}
     E←{
         p2←(¯3○⍵[3;1]÷⍥(b∘/⊃)⍵[3;2])@((b←⊃0.9999>⍵[3;3])⍨)≠⍨p←¯2○⊃⍵[3;3]
         p1←¯3○(-⍵[1;3]÷⍥(b∘/⊃)⍵[2;3])@(b⍨)(⍵[2;1]÷⍥((~b)∘/⊃)⍵[1;1])@(~b⍨)p ⋄ p1 p p2
     }
     U←{
         esu←m.e×(××m.hs*⍨|)rsu←(⊢×1⌊tsu÷⌈/∘∊)(msu÷⍤1 0⊢tsu)+.×⍤1⊂¨iu⊆[1]↑[÷2]⍵
         ju←,⌿ku+t×(⊃+/)¨(tsu÷⍨⍤0 2∘.×⍤1⍨msu)×⍤2 0⊢m.hs×m.e×(××(m.hs-1)*⍨|)rsu ⋄ esu ju
     }
     C←{
         bd ju←⍺ ⋄ j←vu+.×⍨↑ju+.×⊃⍵ ⋄ biu∘←+⌿(↑bd)+.×⍤2⊢vuvd∘.(+.×)i6-⊃⍵
         bu←(↓[1]+.×∘⌹vu+.×⊢)(↑[÷2]ju){(j+⍵)⌹⍨↑j+⍺}⍤2⊢jiu←{j+.×⍵⌹i6-⍵}⍤2↑[÷2]biu ⋄ bu jiu j
     }⍣{i1+←1 ⋄ (max=i1)∨tol>w∘←⌈/∊|⍺(-÷⌈⍥|)⍥⊃⍵}∘{i1∘←0 ⋄ ⍵}
     V←{((⊃¨((2⍴¨⍳3),(2 3)(1 3)(1 2))⌷¨⊂)2÷⍨⍺⍺∘⍉⍨)⍤2⊢⍵}
     W←{
         w3←2÷⍨+.×∘⍉⍨3 3⍴0 1 ¯1 1 0 ¯1 1 ¯1 0×(0,⍨(3↓t×,⌿(⊃+/)¨(-V ssu)×⍤1 0⊢⍵)-⍺+.×wo+.×⍉⍺)[9⍴⌽⍳4]
         ⍺+.×i3+(ww×1○wu)+ww+.×(ww←w3÷wu)×1-2○wu←(÷2)*⍨(⊂1 1)(⊂2 2)(⊂3 3)+.⊃⊂w3
     }
     S←min{⍺⍺⌈1⌊⍺×⍵⍵*1-2×>/⍵}inc{
         su bu _←⍵ ⋄ esu ju←U au+su
         bu jiu j←⍺ ju C bu ⍬ ⋄ jiu←(i6-↓[1]+.×∘⌹vu+.×i6-⍤2⊢)jiu
         ∆s ∆e←bo(~bo)×so(vo×t)-s e∘←(vu+.×⍨↑)¨su(eu←(su+.×⍨,⌿ku)+t×,⌿(⊃+/)¨msu×⍤1 0⊢esu)
         ∆1←bu+.×x1∘←∆s+j⌹⍨∆e ⋄ ∆2←(j+jiu)↓[1]⍤(⌹⍨⍤¯1)⍥(↑[÷2])x2∘←(e-eu)+jiu+.×s-su
         x1 x2∘←⌈/⍤|¨x1(⌈/⍤|¨x2) ⋄ su+←⊃(r1∘←r1 ⍺⍺ x2 y2)(r2∘←r2 ⍺⍺ x1 y1)+.×∆1 ∆2 ⋄ su bu esu
     }⍣{i+←1 ⋄ r←(max=i)∨tol∧.>x1 x2÷⌈/|t×vo ⋄ y1∘←x1 ⋄ y2∘←x2 ⋄ r}∘{i∘←0 ⋄ ⍵}
     D←{
         t bo vo wo so au bu←⍵ ⋄ r1 y1 x1 r2 y2 x2∘←1 ⋄ so-←bo×(vu+.×⍨↑au)-so
         su bu esu←(bd×⊂¨(⊢÷+/)1⌽2×/4⍴1 1⍉fu)S(=⍨au)bu ⍬
         tsu+←m.(⊃H/)↓⍉↑osu(t×esu) ⋄ osu+←t×|esu ⋄ fu+.×⍨←i3+t×,⌿(⊃+/)¨ssu×⍤2 0⊢esu ⋄ ru W←esu
         su bu s e i
     }
     m vuvd iu vol ea tol max←⍺ ⋄ n t bo vo wo so←⍵ ⋄ osu←≠⍨tsu←m.z ⋄
     fu←i3×⊂1⍴⍨≢vol
     vuvd iu vu ru←{(⍵∘{⍵[⍺;⍺]}¨(⊢÷⍤1 0+/)¨vuvd)(iu[⍵])((⊢÷+/)vol[⍵])(M(⊂⊂⍵)⌷¨ea)}⍋iu
     ku ssu←m{((↑⍺.k)R⍨⍤2⊢⍵)((↑⍺.ss)(⊢+.×+.×∘⍉)⍤2⊂¨⍵)}iu⊆[1]↑[÷2]ru ⋄ msu←+V ssu
     at←0 ⋄ as←ae←6⍴0 ⋄ r←1 14⍴0 ⋄ r(E ru)fu⊣{
         su bu s e i←D t bo vo wo so,⍵ ⋄ at+←t ⋄ au+←su ⋄ (as ae)+←s e ⋄ r⍪←at,ae,as,i ⋄ au bu
     }⍣n⊢(au←↓6(≢vol)⍴0)(i6×⊂(≢vol)⍴÷2)
 }
