 CPFEH←{
     i3 i6←∘.=⍨∘⍳¨3 6 ⋄ ij←(2 1 1)(3 3 2) ⋄ bd←(,/(-⍳3)∘.⌽(3↑2)(1 0 1))×⍤1¨⊂i6÷2            ⍝ constants and helpers
     _←{((⊃¨⍵⍵⌷¨⊂)2÷⍨⍺⍺∘⍉⍨)⍤2⊢⍵}((2⍴¨⍳3),↓⍉↑ij) ⋄ T←×∘(,⌿(⊃+/)¨)
     M←{a b c d e f←,1 2∘.○⍵                                                                ⍝ matrix from euler angles
         3 3⍴((d×f)-e×a×c)(-(d×c)+e×f×a)(a×b)((f×a)+d×e×c)((d×e×f)-a×c)(-d×b)(b×c)(f×b)e}
     E←{p2←(¯3○⍵[3;1]÷⍥(b∘/⊃)⍵[3;2])@((b←⊃0.9999>⍵[3;3])⍨)≠⍨p←¯2○⊃⍵[3;3]                    ⍝ euler angles from matrix
         p1←¯3○(-⍵[1;3]÷⍥(b∘/⊃)⍵[2;3])@(b⍨)(⍵[2;1]÷⍥((~b)∘/⊃)⍵[1;1])@(~b⍨)p ⋄ p1 p p2}
     R←{(r w)(i j)←⍵ ij ⋄ r[⍳3;⍺]←×⍨w[⍺;⍳3] ⋄ r[⍺;3+⍳3]←2*w[i;⍺]×w[j;⍺]                     ⍝ rotate voigt vec or matrix
         r[3+⍳3;⍺]←w[⍺;i]×w[⍺;j] ⋄ r[3+⍳3;3+⍺]←w[i[⍺];i]×w[j[⍺];j]+w[j[⍺];i]×w[i[⍺];j]
         r w}{⊃⊃⍺⍺/(⍳3),⊂(6 6⍴≠⍨⊃⍵)⍵}{(⍺⍺⍉⍺)+.×⍵+.×⍵⍵×⍺⍺ ⍺}(⊃⍪⌿,/3 3∘⍴¨2 2⍴1 0.5 2 1)
     W←{w←0 1 ¯1 1 0 ¯1 1 ¯1 0×(0,⍨(3↓t T(-_ ssu)×⍤1 0⊢⍵)-⍺+.×wo+.×⍉⍺)[9⍴⌽⍳4]               ⍝ rotate orientation matrices
         ⍺+.×i3+(w×1○u)+w+.×(w←w÷u)×1-2○u←(÷2)*⍨+/1 1⍉w←↑(÷2)×(⊢×3-+.×⍨)¨↓3 3⍴w}
     S←{b⌈t⌊⍺×i(÷⊢/b t i d←conv)[1+>/⍵]}{so t←⍺ ⋄ g hs←m.g m.hs                             ⍝ grain stress
         esu∘←g×(××hs*⍨|)rsu∘←(msu÷⍤1 0⊢tsu)+.×⍤1⊂¨iu⊆[1]↑[÷2]au+⍵                          ⍝   local slip shear
         ju∘←,⌿ku+t×+/↑(tsu÷⍨⍤0 2∘.×⍤1⍨msu)×⍤2 0⊢hs×g×(××(hs-1)*⍨|)rsu                      ⍝   local behaviour
         jv∘←((j←vu+.×⍨↑ju+.×bu)+.×⊢⌹i6-⊢)⍤2↑[÷2]+⌿(↑bs)+.×⍤2⊢vuvd∘.(+.×)i6-bu              ⍝   interaction
         bv∘←(↓[1]+.×∘⌹vu+.×⊢)(↑[÷2]ju)((j+⊢)⌹⍨∘↑j+⊣)⍤2⊢jv∘←(i6-⍤2⊢+.×∘⌹vu+.×i6-⍤2⊢)jv      ⍝   localisation tensors
         ∆←⊃bo(~bo)(⊢+j+.×⊣).×so(vo×t)-s e∘←(vu+.×⍨↑)¨⍵(eu←(⍵+.×⍨,⌿ku)+t T msu×⍤1 0⊢esu)    ⍝   global error
         ∆u∘←(e-eu)+(↓[1]jv)+.×s-⍵ ⋄ x xu∘←⌈/⍤|¨∆(⌈/⍤|¨∆u) ⋄ d ⍺⍺¨←(xu yu)(x y)             ⍝   local error and factor
         ym∘←⌈/∊|bu(-÷⌈⍥|)⍥⊃bv ⋄ ⍵+⊃d+.×((bu∘←bv)+.×j⌹⍨∆)(↓[1](j+⍤2⊢jv)⌹⍨⍤¯1↑[÷2]∆u)        ⍝   correction
        }⍣{(×÷max-is∘←is+1)∧tol∧.>ym,(y yu∘←x xu)÷t×⌈/|vo}∘{d y yu∘←1 ⋄ is∘←0 ⋄ ⍵}
     tol min max←⍺⍺ ⋄ conv←⍵⍵ ⋄ n t bo vo wo so←⍺ ⋄ m←∪⊃mat ea area vol←4↑⍵                 ⍝ parameters
     iu vuvd vu ru←{⍺[⍵](⍵ ⍵∘⌷¨(⊢÷⍤1 0+/)¨area)((⊢÷+/)vol[⍵])(M(⊂⊂⍵)⌷¨ea)}∘⍋⍨m⍳mat          ⍝ microstructure
     ku ssu←m{((↑⍺.k)R⍨⍤2⊢⍵)((↑⍺.ss)(⊢+.×+.×∘⍉)⍤2⊂¨⍵)}iu⊆[1]↑[÷2]ru ⋄ msu←+_ ssu            ⍝ behaviour
     at ae as au asu tsu fu←(≢vu){0(6⍴0)(6⍴0)(↓6 ⍺⍴0)0 m.z(i3×⊂⍺⍴1)}⍣(4=≢⍵)⊢4↓⍵             ⍝ initialise
     su bu∘←(≢vu){(↓6 ⍺⍴0)i6}⍣(13≠≢⍵)⊢11↓⍵ ⋄ (E ru)fu,⍨⊂{bs∘←bd×⊂¨(⊢÷+/)1⌽2×/4⍴1 1⍉fu       ⍝ simulation
        su bu is∘←((2×so)-vu+.×⍨↑au)t{min>⊃⌽⍺: ⍵ ⋄ su bu∘←2↑⍵ ⋄ 11:: (⍺÷2)∇⍣2⊢⍵+0 0 is      ⍝   divide & conquer
            at ae as au+←t e s,⊂su←⍺ S su ⋄ is∘←is+⊃⌽⍵ ⋄ su bu is}su bu 0                   ⍝   solve stress
        tsu+←m.(⊃H/)↓⍉↑asu(t×esu) ⋄ asu+←t×|esu ⋄ fu+.×⍨←i3+t T ssu×⍤2 0⊢esu ⋄ ru W←esu     ⍝   update
        ⍵⍪at,ae,as,is}⍣n⊢1 14⍴0
 }