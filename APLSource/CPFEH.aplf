 CPFEH←{
     i3 i6←∘.=⍨∘⍳¨3 6 ⋄ ij←(2 1 1)(3 3 2) ⋄ bd←(i6×⍤1⊢)¨~↓,∘~⍨i3                             ⍝ constants and helpers
     _←{((⊃¨⍵⍵⌷¨⊂)2÷⍨⍺⍺∘⍉⍨)⍤2⊢⍵}((2⍴¨⍳3),↓⍉↑ij) ⋄ T←×∘(,⌿(⊃+/)¨)
     M←{a b c d e f←,1 2∘.○⍵                                                                 ⍝ matrix from euler angles
         3 3⍴((d×f)-e×a×c)(-(d×c)+e×f×a)(a×b)((f×a)+d×e×c)((d×e×f)-a×c)(-d×b)(b×c)(f×b)e}
     E←{p2←(¯3○⍵[3;1]÷⍥(b∘/⊃)⍵[3;2])@((b←⊃0.9999>⍵[3;3])⍨)≠⍨p←¯2○⊃⍵[3;3]                     ⍝ euler angles from matrix
         p1←¯3○(-⍵[1;3]÷⍥(b∘/⊃)⍵[2;3])@(b⍨)(⍵[2;1]÷⍥((~b)∘/⊃)⍵[1;1])@(~b⍨)p ⋄ p1 p p2}
     R←{(r w)(i j)←⍵ ij ⋄ r[⍳3;⍺]←×⍨w[⍺;⍳3] ⋄ r[⍺;3+⍳3]←2*w[i;⍺]×w[j;⍺]                      ⍝ rotate voigt vec or matrix
         r[3+⍳3;⍺]←w[⍺;i]×w[⍺;j] ⋄ r[3+⍳3;3+⍺]←w[i[⍺];i]×w[j[⍺];j]+w[j[⍺];i]×w[i[⍺];j]
         r w}{⊃⊃⍺⍺/(⍳3),⊂(6 6⍴≠⍨⊃⍵)⍵}{(⍺⍺⍉⍺)+.×⍵+.×⍵⍵×⍺⍺ ⍺}(⊃⍪⌿,/3 3∘⍴¨2 2⍴1 0.5 2 1)
     W←{w←0 1 ¯1 1 0 ¯1 1 ¯1 0×(0,⍨(3↓t T(-_ ssu)×⍤¯1⊢⍵)-⍺+.×wo+.×⍉⍺)[9⍴⌽⍳4]                 ⍝ rotate orientation matrices
         ⍺+.×i3+(w×1○u)+w+.×(w←w÷u)×1-2○u←(÷2)*⍨+/1 1⍉w←↑(÷2)×(⊢×3-+.×⍨)¨↓3 3⍴w}
     S←⊃{b⌈t⌊⍺×i(÷⊢/b t i d←conv)[1+>/⍵]}{so t←⍺ ⋄ su x d∘←⍵                                 ⍝ grain stress
         esu∘←g×(××hs*⍨|)rsu∘←(msu÷⍤¯1⊢tsu)+.×⍤1⊂¨iu⊆[1]↑[÷2]au+su                           ⍝   local slip shear
         ju∘←,⌿ku+t×+/↑(tsu÷⍨⍤0 2∘.×⍤1⍨msu)×⍤¯1⊢hs×g×(××(hs-1)*⍨|)rsu                        ⍝   local behaviour
         jv∘←(i6-⍤2⊢+.×∘⌹vu+.×i6-⍤2⊢)((j←vu+.×⍨↑ju+.×bu)+.×i6∘-⌹⊢)⍤2↑[÷2]bu                  ⍝   interaction
         ∆←⊃bo(~bo)(⊢+j+.×⊣).×so(vo×t)-s e∘←(vu+.×⍨↑)¨su(eu∘←(su+.×⍨,⌿ku)+t T msu×⍤¯1⊢esu)   ⍝   global error
         ∆u∘←(e-eu)+(↓[1]jv)+.×s-su ⋄ d ⍺⍺¨←(x∘←⌈/⍤|¨∆(⌈/⍤|¨∆u)),¨⍥⌽x                        ⍝   local error and factor
         (su+⊃d+.×(bu+.×j⌹⍨∆)(↓[1](j+⍤2⊢jv)⌹⍨⍤¯1↑[÷2]∆u))x d                                 ⍝   correction
     }⍣{(÷max>i∘←i+1)∧tol∧.>x÷t×⌈/|vo}∘{i∘←0 ⋄ ⍵ tol,conv[2]}
     tol min max conv←⍺⍺ ⋄ n t bo vo wo so←⍺ ⋄ m←∪⊃mat ea area vol←4↑⍵ ⋄ g hs←m.g m.hs       ⍝ parameters
     iu vuvd vu ru∘←{⍺[⍵](⍵ ⍵∘⌷¨(⊢÷⍤¯1+/)¨area)((⊢÷+/)vol[⍵])(M(⊂⊂⍵)⌷¨ea)}∘⍋⍨m⍳mat           ⍝ microstructure
     at ae as au asu tsu fu∘←7↑(≢vu){0(6⍴0)(6⍴0)(↓6 ⍺⍴0)0(m.H 0)(i3×⊂⍺⍴1)}⍣(4=≢⍵)⊢4↓⍵        ⍝ initialise
     su bu∘←(≢vu){(↓6 ⍺⍴0)(i6÷2)}⍣(13≠≢⍵)⊢11↓⍵ ⋄ vdu←0 ⋄ (E ru)fu,⍨⊂{                        ⍝ simulation
         tol>⌈/|∊⍺-⍵:0 ⍺ bu ⋄ 1 ⍵((↓+.×∘⌹vu+.×⍨⊢)↑i6-+⌿(↑bd×⊂¨⍵)+.×⍤2⊢vuvd∘.(+.×)i6-bu)}{    ⍝   localisation
         ku ssu∘←m{((↑⍺.k)R⍨⍤2⊢⍵)((↑⍺.ss)(⊢+.×+.×∘⍉)⍤2⊂¨⍵)}iu⊆[1]↑[÷2]ru ⋄ msu∘←+_ ssu       ⍝   behaviour
         ib vdu bu∘←vdu ⍺⍺(⊢÷+/)1⌽2×/4⍴1 1⍉fu ⋄ su i∘←((2×so)-vu+.×⍨↑au)t{min>⊃⌽⍺:⍵          ⍝   localisation
             11::(⍺÷2)∇⍣2⊢⍵+0 i ⋄ at ae as au+←t e s(su←⍺ S⊃⍵) ⋄ su(i+⊃⌽⍵)}su 0              ⍝   solve stress
         tsu+←m.(⊃H/)↓⍉↑asu(t×esu) ⋄ asu+←t×|esu ⋄ fu+.×⍨←i3+t T ssu×⍤¯1⊢esu ⋄ ru W←⊂esu     ⍝   update
         ⍵⍪(⍵⍵⊢⊢)at,ae,as,i,ib}⍵⍵⍣n⊢1 15⍴0
 }