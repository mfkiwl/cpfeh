:Namespace EULER                                                     ⍝ Orientations and misorientations
    c←↑(1 2 3 4)(2 1 4 3)(3 4 1 2)(4 3 2 1)                          ⍝ product components
    u←↑(1 ¯1 ¯1 ¯1)(1 1 1 ¯1)(1 ¯1 1 1)(1 1 ¯1 1)                    ⍝ product unit factors
    QP←+/u×⊣(×⍤1)(⊂c)⌷⊢ ⋄ QC←×∘(1,-3⍴1) ⋄ UV←×∘(÷(÷2)*⍨+.×⍨)⍨        ⍝ product, conjugate and unitary vector
    QA←((⊂2○⊢),(⊂1○⊢)∘.×∘UV⊣)∘(÷∘2) ⋄ QE←⊃(3⍴¯3 3↑¨1)QP.QA⊢          ⍝ quaternion from axis-angle and zxz Euler angles
    cs←⊂1 0 0 0 ⋄ cs,←,(1 0 0)(0 1 0)(0 0 1)∘.QA○1 2 3÷2             ⍝ identity and 4-fold symmetry around <001>
    cs,←,(1 1 1)(¯1 1 1)(1 ¯1 1)(1 1 ¯1)∘.QA○2 4÷3                   ⍝ 3-fold symmetry around <111>
    cs,←,(1 1 0)(1 0 1)(0 1 1)(1 ¯1 0)(¯1 0 1)(0 1 ¯1)∘.QA○1         ⍝ 2-fold symmetry around <110>
    MC←⊃2×¯2○1⌊cs⌈.(|+.×)∘⊂QC⍤QP∘QC ⋄ ML←⌊○⍤⊣÷⍨180×⊢                 ⍝ misorientation (cubic symmetry) and level
    M←{ IM←⌈(⊢+2÷⍨(1-⍨⊣)×2-⍨⊣)⌊ ⋄ CM←{(c←⎕NS⍬).m←0⍴⍨⍵IM.-0 1 ⋄ c}    ⍝ index and cache of misorientations
        _M_ ← {(c s)←⍺⍺ ⍵⍵ ⋄ (a w)←(⊂,d←⍺≠⍵)/∘,¨⍺ ⍵ ⋄ 0=≢a:d         ⍝ memoization
            n←(≠,i)∧s.m[i←a IM w]<1 ⋄  0=≢p←n/⍥,i:s.m[i]@⊢d          ⍝   return calculated if all done
            s.m[p]←ML⊃a MC⍥{↓⍉↑c[n/⍵]}w ⋄ s.m[i]@⊢d}                 ⍝   calculate and return
        m←⎕NS'MC' 'IM' ⋄ m.ML←⍺∘ML ⋄ m.M←1+w _M_(CM≢w←↓⍉↑⍵) ⋄ m}     ⍝ namespace with M and curried ML
:EndNamespace